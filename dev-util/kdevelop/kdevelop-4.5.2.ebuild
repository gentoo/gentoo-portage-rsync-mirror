# Copyright 1999-2015 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-x86/dev-util/kdevelop/kdevelop-4.5.2.ebuild,v 1.7 2015/01/29 20:38:34 johu Exp $

EAPI=5

KDE_LINGUAS="bs ca ca@valencia da de el en_GB es et fi fr gl hu it kk nb nds nl
pl pt pt_BR ru sk sl sv th tr uk zh_CN zh_TW"
VIRTUALX_REQUIRED=test
inherit kde4-base

DESCRIPTION="Integrated Development Environment for Unix, supporting KDE/Qt, C/C++ and many other languages"
LICENSE="GPL-2 LGPL-2"
IUSE="+cmake +cxx debug okteta qthelp reviewboard"
SRC_URI="mirror://kde/stable/kdevelop/${KDEVELOP_VERSION}/src/${P}.tar.xz"

if [[ $PV == *9999* ]]; then
	KEYWORDS=""
else
	KEYWORDS="amd64 ppc x86"
fi

DEPEND="
	>=dev-util/kdevplatform-${KDEVPLATFORM_VERSION}[reviewboard?]
	$(add_kdebase_dep ksysguard)
	$(add_kdebase_dep libkworkspace)
	okteta? ( $(add_kdebase_dep okteta) )
	qthelp? ( dev-qt/qthelp:4 )
	reviewboard? ( dev-libs/qjson )
"
RDEPEND="${DEPEND}
	$(add_kdebase_dep kapptemplate)
	$(add_kdebase_dep kdebase-kioslaves)
	dev-qt/qtdeclarative:4[webkit]
	cxx? ( >=sys-devel/gdb-7.0[python] )
"
RESTRICT="test"
# see bug 366471

PATCHES=( "${FILESDIR}/${P}-kdevplatform-without-qjson.patch" )

src_configure() {
	mycmakeargs=(
		$(cmake-utils_use_build cmake)
		$(cmake-utils_use_build cmake cmakebuilder)
		$(cmake-utils_use_build cxx cpp)
		$(cmake-utils_use_with okteta LibKasten)
		$(cmake-utils_use_with okteta LibOkteta)
		$(cmake-utils_use_with okteta LibOktetaKasten)
		$(cmake-utils_use_build qthelp)
		$(cmake-utils_use_find_package reviewboard QJSON)
	)

	kde4-base_src_configure
}
