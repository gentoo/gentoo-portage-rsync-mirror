--- configure.ac	2012-03-12 20:18:21.000000000 +0200
+++ configure.ac	2012-03-14 22:03:15.854332138 +0200
@@ -82,7 +82,27 @@
 
 # Checks for library functions.
 AC_CHECK_FUNCS(signal select ngettext gethostbyname getpwnam getuid)
-AC_CHECK_LIB(resolv, res_query,, AC_MSG_WARN([[no libresolv, SRV RRs not used]]))
+
+# libresolv provides only __res_query on some architectures
+# so we cannot just use AC_CHECK_LIB
+_save_LIBS="$LIBS"
+LIBS="$LIBS -lresolv"
+AC_MSG_CHECKING(for res_query in -lresolv)
+AC_LINK_IFELSE([
+#include <resolv.h>
+int main()
+{
+  res_query (0, 0, 0, 0, 0);
+  return 0;
+}],
+[_res_query_in_resolv=yes],
+[_res_query_in_resolv=no])
+AC_MSG_RESULT([$_res_query_in_resolv])
+if test "x$_res_query_in_resolv" = "xno"; then
+  LIBS="$_save_LIBS"
+  AC_MSG_WARN([[no libresolv, SRV RRs not used]])
+fi
+
 AC_SEARCH_LIBS(socket, socket)
 AC_SEARCH_LIBS(gethostbyname, nsl)
 
